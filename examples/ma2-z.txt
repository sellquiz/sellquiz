%course Mathematik 2 - Knospe, Randerath
%quiz Zusammenfassendes Kurzquiz


Komplexe Zahlen

	a, b in { 1, 2, 3 }
	x := a * a - b * b
	y := 2 * a * b
	z := x + y * i
	res := abs(z)

Sei $ "z" in CC $ und $ "z" = z $. Berechne:
$ abs("z") = #res $
$ "z" * "z" = #(z*z) $
$ bar "z" = #(x-y*i) $


%%%

Komplexe Zahlen

Seinen $z, z_1, z_2 in CC$. Wähle alle richtigen Antworten aus:
[x] $bar bar z = z$
[x] $z=bar z hArr z in RR$
[x] $i^12345678 = -1$
[x] $bar(z_1+z_2) = bar z_1 + bar z_2$


%%%


Komplexe Reihen

	a,b in { 2, 3, ..., 6 }

Ist die folgende geometrische Reihe __absolut konvergent__?

* $ sum_(k=0)^(oo) (1/a + 1/b*i)^k $

(x) ja
( ) nein


%%%


Konvergenzradius

	a,b in { 2, 3, ..., 10 }
	z1 := a + b*i
	res := 1

Bestimmen Sie den __Konvergenzradius__ der Potenzreihe $ sum_(k=0)^(oo) k * (z1) * z^k $

* $ R = #res $


%%%


Komplexe Potenzen

	a in { 1, 2 }
	c in { 4, 5, ..., 8 }
	z := a + a*i
	res := z^c

Berechnen Sie $(z)^c$ unter Verwendung der Exponentialform:

* $ #res $


%%%


Komplexe Nullstellen

    a1, a2, b1, b2 in { 3, 4, 5 }
    a = a1 + i*a2
    b = b1 + i*b2
    c in { 5, 6, 7 }
    d in { 8, 9, 10 }

Wie viele komplexe Nullstellen besitzt das Polynom $(a)*z^c + (b)*z^d$ mit $c in CC$?
$ #d $


%%%


Lineare DGL erster Ordnung mit konstanten Koeffizienten

    a in { 3, 4, 5 }

Wähle die richtige Lösung zur DGL $y'(x) = a*y(x)$:
(x) $y(x)=C e^(a x), \  C in RR$
( ) $y(x)=C e^(-a x), \  C in RR$
( ) $y(x)= e^(a x)$
( ) $y(x)=e^(-a x)$


%%%


Lineare DGL erster Ordnung mit konstanten Koeffizienten

    a, b in { 3, 4, 5 }

Gegeben sei die DGL erster Ordnung $y'(x) = a*y(x) + b x$.
Welcher Ansatz für eine spezielle Lösung des inhomogenen Systems ist der Richtige?
(x) $y(x) = A_0 + A_1 x$
( ) $y(x) = A_0$
( ) $y(x) = A_0 x$
( ) $y(x) = b x$


%%%


Lineare DGL erster Ordnung mit konstanten Koeffizienten

    a, b, c in { 3, 4, 5 }

Gegeben sei die DGL erster Ordnung $y'(x) = a*y(x) + b cos(c x)$.
Welcher Ansatz für eine spezielle Lösung des inhomogenen Systems ist der Richtige?
(x) $y(x) = A_0 cos(c x) + B_0 sin(c x)$
( ) $y(x) = A_0 cos(c x)$
( ) $y(x) = A_0 sin(c x)$


%%%


Lineare DGL zweiter Ordnung mit konstanten Koeffizienten

    a in {2, 3, ..., 5}
    b in {4, 5, ..., 8}
    c(x) := a/2 + sqrt((a/2)^2 + b)
    d(x) := a/2 - sqrt((a/2)^2 + b)

Wähle die richtige Lösung zur DGL $y''(x) + a*y'(x) - b*y(x) = 0$:
(x) $y(x)=C_1 e^(c x) + C_2 e^(d x), \  C_1, C_2 in RR$
( ) $y(x)=C_1 e^(c x) + C_2 x e^(d x), \  C_1, C_2 in RR$
( ) $y(x)=C_1 e^(a x) + C_2 e^(-b x), \  C_1, C_2 in RR$


%%%


Restklassen

Geben Sie jeweils den __Standardrepräsentaten__ an:

	a, b, c in { 10, 11, ..., 20 }
	a2, b2, c2, c3 in { 2, 3, ..., 5 }
	sa := a mod a2
	b := - b
	sb := b mod b2
	sc := c2*c3 mod c2

* $ ZZ_c2 $ : $ c2*c3 equiv #sc mod c2 $
* $ ZZ_a2 $ : $ a equiv #sa mod a2 $
* $ ZZ_b2 $ : $ b equiv #sb mod b2 $


%%%


Körper

Welche der folgenden Restklassen sind __Körper__?

[x] $ ZZ_3 $
[ ] $ ZZ_4 $
[x] $ ZZ_7 $
[ ] $ ZZ_10 $


%%%


Multiplikativ inverses Element

	n in { 3, 5, 7 }
	b in { 1, 2, ..., 7 }
	c := b + 1
	b := b mod (n-1) + 1
	c := c mod (n-1) + 1

	s1 := xgcd(b, n, 2) mod n    % 2 := get 2nd parameter of xgcd
	%TODO: enable solution hints again... Hinweis: $(b * x) mod n equiv 1 $. Bitte geben Sie den Standardrepräsentaten an.

	s2 := xgcd(c, n, 2) mod n
	%TODO: enable solution hints again... %Hinweis: $(c * x) mod n equiv 1 $. Bitte geben Sie den Standardrepräsentaten an.

Geben Sie die __multiplikativ inversen Elemente__ für den Körper $GF(n)$ an:

* $b^(-1) equiv #s1 mod n$
* $c^(-1) equiv #s2 mod n$


%%%


Untervektorräume

Welche der folgenden Mengen sind Untervektorräume des $GF(2)^2$?
[ ] $ M_1 = { [[1],[1]] } $
	%TODO:	$ 0 in K, u in U $ -> $ 0 * u notin U$
[x] $ M_2 = { [[0],[0]] } $
[x] $ M_4 = { lambda * [[0],[1]] | lambda in GF(2) } $


%%%


Erzeugendensysteme

	% TODO: vector operations...

	a, b, c, d, ee, f in { 0, 1 }
	a := 1
	d := 0
	ee := 1
	l1 := 0
	l2 := 1
	l3 := 1
	l4 := 1

	g := (a*l1 + d*l2) mod 2
	h := (b*l1 + ee*l2) mod 2
	i_ := (c*l1 + f*l2) mod 2

	j_ := (a*l3 + d*l4) mod 2
	k := (b*l3 + ee*l4) mod 2
	l := (c*l3 + f*l4) mod 2

Seien $ v_1 = [[a],[b],[c]] $, $ v_2 = [[d],[ee],[f]] $ Vektoren im $ GF(2)^3 $ und $ U = lt v_1, v_2 gt $.

Welche der folgenden Antworten sind richtig?

[x] $ w_1 = [[g],[h],[i_]] in U $
[x] $ w_2 = [[j_],[k],[l]] in U $


%%%


Determinante

	a := { -5, -4, ..., 5 }
	A in MM( 3 x 3 | a )
	A[1,1] := 0
	A[1,2] := 0
	d := det(A)

Sei $ "A" = A $ eine 3 x 3 Matrix über $ K = RR $.
Berechnen Sie die __Determinante__:

* $ det("A") = #d $


%%%


Inverse Matrix

	a := { 0, 1 }
	A in MM( 3 x 3 | a, invertible )
	iA := inv(A)

Berechnen Sie die __inverse__ Matrix:

* $ A^(-1) = #iA $


%%%


Lineare Unabhängigkeit

	a, b, c, d, e, f, g, h, _i in { 0, 1 }

	v1 := [[a],[b]]
	v2 := [[c],[d]]
	A := [[a,c],[b,d]]     % TODO: concatenate vectors v1 and v2
	q1 := (det(A) mod 2) != 0

	a := (a + 1) mod 2
	v3 := [[a],[b]]
	v4 := [[c],[d]]
	A := [[a,c],[b,d]]     % TODO: concatenate vectors v3 and v4
	q2 := (det(A) mod 2) != 0

	v5 := [[a],[b],[c]]
	v6 := [[d],[e],[f]]
	v7 := [[g],[h],[_i]]
	A := [[a,d,g],[b,e,h],[c,f,_i]]
	q3 := (det(A) mod 2) != 0

Welche der unten stehenden Vektoren über dem $ GF(2) $ sind __linear unabhängig__?

[q1] $ v1, v2 $
[q2] $ v3, v4 $
[q3] $ v5, v6, v7 $


%%%


Eigenwerte

	A in MM(2 x 2 | { 1, 2, 4 }, invertible, symmetric)
	lambda = eigenvalues_sym(A)

Sei $ "A" = A $.

Geben Sie die __Eigenwerte__ von $"A"$ an:

* $ "lambda" = #lambda $

_Hinweis: Geben Sie bei Bedarf `sqrt(x)` für $ sqrt(x) $ an._


%%%


Gradient

	a, b in { 0, 1 }
	c in { 2, 3, ..., 5}

	f(x,y) := - x^3 - a*y^3 + b*c*x*y
	fx(x,y) := diff(f, x)
	fy(x,y) := diff(f, y)

Gegeben sei eine Funktion $ "f"(x,y) = f $.
Bestimmen Sie den __Gradienten__:
* $ grad "f" = ( #fx, #fy ) $


%%%


Rotation und Divergenz

	a, b in { 0, 1 }
	c := 1 - a
	d := 1 - b
	ee in { 2, 3, ..., 9 }
	f1(x,y,z) := a * x + c * z
	f2(x,y,z) := b * sin(pi*y) + d * cos(pi*x)
	f3(x,y,z) := x^3 + ee * z^2

Sei $f(x,y) = ( f1, f2, f3 )$.
Bestimmen Sie __Rotation__ und __Divergenz__ des Vektorfeldes.

	rx(x,y,z) := diff(f3,y) - diff(f2,z)
	ry(x,y,z) := diff(f1,z) - diff(f3,x)
	rz(x,y,z) := diff(f2,x) - diff(f1,y)

* rot$(f) = ( #rx, #ry, #rz )$

	d(x,y,z) := diff(f1,x) + diff(f2,y) + diff(f3,z)

* div$(f) = #d$


%%%


Definitheit

	a, b, c in { -1, 1, 2 }
	A := [[a,0,0],[0,b,0],[0,0,c]]
	s := { a, b, c }
	p1 := min(s) > 0
	p2 := max(s) < 0
	p3 := not (p1 or p2)

Sei $ "A" = A $ eine Matrix über $ RR $.
Welche der folgenden Aussagen ist korrekt?
[p1] $"A"$ ist __positiv definit__
[p2] $"A"$ ist __negativ definit__
[p3] $"A"$ ist __indefinit__



%%%


Hesse-Matrix

	a in { 2, 3, ..., 8 }
	b in { 2, 3, 4 }
	f(x,y) := a * x^b * y^(b+1)
	h11(x,y) := diff( diff(f,x), x)
	h12(x,y) := diff( diff(f,x), y)
	h21(x,y) := diff( diff(f,y), x)
	h22(x,y) := diff( diff(f,y), y)
	H := [ [ h11, h12 ], [ h21, h22 ] ]

Sei $"f"(x,y) = f$.
Bestimmen Sie die __Hesse-Matrix__:
* $ "H" "f" = #H $


%%%


Extremwerte

	%f(x,y) := -x^3 - y^3 + 3*x*y
	w1 in { 0, 1 }
	w2 := 1 - w1
	w3, w4, w5, w6, w7, w8, w9, w10 in { 2, 3 }
	f(x,y) := w4*x^w5*y^w6 + w7*x^w8 + w9*y^w10
	h11(x,y) := diff( diff(f,x), x)
	h12(x,y) := diff( diff(f,x), y)
	h21(x,y) := diff( diff(f,y), x)
	h22(x,y) := diff( diff(f,y), y)
	H := [ [ h11, h12 ], [ h21, h22 ] ]
	ax := 0
	ay := 0
	A := [ [ h11(ax,ay), h12(ax,ay) ], [ h21(ax,ay), h22(ax,ay) ] ]
	lambda := eigenvalues_sym(A)
	p1 := min(lambda) > 0
	p2 := max(lambda) < 0
	p3 := not (p1 or p2)

Sei $"f"(x,y) = f$.
Bestimmen Sie die __Hesse-Matrix__:
* $ "H" "f" = #H $

%* _Hinweis: geben Sie die Exponentialfunktion in der Form `exp(x)` an._

Betrachten Sie die potentielle Extremstelle $ a=(ax,ay) $ und berechnen Sie die Eigenwerte von $ ("H" "f")(a) $:
* $ "lambda" = #lambda $

Welche der folgenden Aussagen ist korrekt?
[p1] $"f"$ besitzt bei $a$ ein __lokales Minimum__
[p2] $"f"$ besitzt bei $a$ ein __lokales Maximum__
[p3] $"f"$ besitzt bei $a$ einen __Sattelpunkt__
