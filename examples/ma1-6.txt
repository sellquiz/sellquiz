%course Mathematik 1 - Knospe, Randerath
%quiz 6: Lineare Algebra


Vektorrechnung

    set := { -9, -8, ..., 9 }
    lambda in set
    u, v in MM(3 x 1 | set )
    w := u + v
    x := lambda * u

Gegeben sind die beiden Vektoren $"u"=u$ und $"v"=v$ im $RR^3$. Berechne:

    input rows := resizable

* $ "u"+"v" = #w $
* $ lambda "u" = #x $


%%%


Einheitsvektor

    k in { 1, 2, 3 }
    v in MM(3 x 1 | {0} )
    v[k,1] := 1
    input rows := resizable

Geben Sie den Einheitsvektor $e_(k)$ im $RR^3$ an:
$#v$


%%%


Rechenregeln

Welche der folgenden Rechenregeln zur Addition und skalaren Multiplikation im $RR^n$ sind korrekt?
[x] $ (u+v)+w = u+(v+w) \ \ AA u, v, w in RR^n $
[x] $ 1 * v = v \ \ AA v in V $
[x] $ (lambda + mu) v = lambda v + mu v \ \ AA lambda, mu in RR, \ \ v in RR^n $
[x] $ v + w = w + v \ \ AA v, w in RR^n $


%%%


Kollinear

    a, b in { -4, -3, -2, 2, 3, 4 }

    ux, uy in { -3, -2, -1, 1, 2, 3 }
    u1 := [[ux],[uy]]
    v1 := a * u1

    ux, uy in { -3, -2, -1, 1, 2, 3 }
    u2 := [[ux],[uy]]
    v2 := [[ux*b],[uy*(-b)]]

Welche der folgenden Ortsvektoren sind __kollinear__?
[x] $ u=u1 $ und $ v=v1 $
[ ] $ u=u2 $ und $ v=v2 $


%%%


Skalarprodukt

    set := { -5, -4, ..., 5 }
    u, v in MM(3 x 1 | set )
    s := dot(u, v)

Gegeben sind die beiden Vektoren $"u"=u$ und $"v"=v$ im $RR^3$.
Berechne das __Skalarprodukt__ $ "u" * "v" = #s $


%%%


Eukidische Norm

Sei $u=[[u_1],[...],[u_n]] in RR^n$. 
Wie ist die __euklidische Norm__ definiert?
(x) $ sqrt(u_1^2 + u_2^2 + ... + u_n^2) $
( ) $ |u_1| + |u_2| + ... + |u_n| $
( ) $ max_(i=1,...,n) |u_i| $


%%%


Eukidische Norm

    ux, uy, uz in { -5, -4, ..., 5 }
    u = [[ux],[uy],[uz]]
    en := sqrt(ux*ux + uy*uy + uz*uz)

Sei $ "u"=u in RR^3 $. Berechne die __euklidische Norm__:
* $ ||"u"|| = #en $ 

_Hinweis: Schreiben Sie (sofern nötig) für $sqrt(x)$ `sqrt(x)`._


%%%


Winkel zwischen Vektoren

    a, b in  { 3, 4, ..., 8 }
    u, v in MM(3 x 1 | {0} )
    u[1,1] := a
    v[1,1] := 3
    v[3,1] := 4
    a := acos( dot(u, v) / ( norm2(u) * norm2(v) ) )

Gegeben sind die beiden Vektoren $"u"=u$ und $"v"=v$ im $RR^3$.
Berechnen Sie den __Winkel__ zwischen den Vektoren $"u"$ und $"v"$:
* $ #a $

_Hinweis: Schreiben Sie (ja nach Bedarf) `asin(x)`, `acos(x)` für die Arkusfunktionen._


%%%


Orthogonale Vektoren

    u, v, w, x, y, z in MM(2 x 1 | {-1,0,1})
    uv := dot(u,v) == 0
    wx := dot(w,x) == 0
    yz := dot(y,z) == 0

Welche der folgenden Vektoren im $RR^2$ sind __orthogonal__?
[uv] $u$ und $v$
[wx] $w$ und $x$
[yz] $y$ und $z$


%%%


Projektion

    w, v in MM(2 x 1 | { -2, -1, 1, 2 })
    p := dot(v,w) / dot(v,v) * v
    input rows := resizable

Berechne die __Projektion__ des Vektors $w$ auf den Vektor $v$:
$ #p $


%%%


Vektorprodukt

In welchen der folgenden euklidischen Räumen kann das Vektorprodukt bestimmt werden?
[ ] $RR^2$
[x] $RR^3$
[ ] $RR^4$


%%%


Vektorprodukt

    set := { -2, -1, 0, 1, 2 }
    u, v in MM(3 x 1 | set )
    uxv := cross(u, v)
    input rows := resizable

Gegeben sind die beiden Vektoren $"u"=u$ und $"v"=v$ im $RR^3$. Bestimme das Vektorprodukt von $"u"$ und $"v"$:
* $"u" xx "v" = #uxv$


%%%


Matrizenoperationen

	a := { 1, 2, 3 }
	A, B in MM(3 x 3 | a )
	C := A - B
    input rows := resizable
    input cols := resizable

Berechne $ A - B = #C $


%%%


Matrizenoperationen

	a in { 2, 3, ..., 5 }
	A in MM(3 x 3 | { 1, 2, 3 } )
	B := a * A
    input rows := resizable
    input cols := resizable

Berechne $ a * A = #B $


%%%


Transponierte Matrix

	A in MM(3 x 2 | { 1, 2, 3 } )
	B := A^T
    input rows := resizable
    input cols := resizable

Bestimme $ A^T = #B $


%%%


Matrizenoperationen

	A, B in MM(2 x 2 | { 1, 2, 3 } )
	C := A * B
    input rows := resizable
    input cols := resizable

Berechne $ A*B = #C $


%%%


Matrizenoperationen

	a := { 1, 2, 3 }
	A in MM( 2 x 1 | a )
	B in MM( 2 x 2 | a )
	C in MM( 2 x 1 | a )
	D := (A + B^T * C)^T
    input rows := resizable
    input cols := resizable

Berechne $ (A + B^T * C)^T = #D $


%%%


Matrizenoperationen

	a := { -5, -4, ..., 5 }
	MA in MM( 2 x 3 | a )
	MB in MM( 3 x 2 | a )
	MC in MM( 2 x 2 | a )

Gegeben seien die folgenden Matrizen:
* $ A=MA, B=MB, C=MC $

Welche der folgenden Operationen sind erlaubt?
[x] $ A * B $
[ ] $ A + B $
[ ] $ A^T * B $
[ ] $ A * B^T $
[x] $ A * B - C $
[x] $ (A * B)^T + C $


%%%


Lineare Gleichungssysteme

    a11, a12, a21, a22 in { 2, 3, 4 }
    b1, b2 in { 1, 2, 3 }
    a21 := 0
    A := [ [a11, a12], [a21, a22] ]
    b := [ [b1], [b2] ]
    x := linsolve(A, b)

    input rows := resizable
    input cols := resizable

Gegeben ist das folgende __lineare Gleichungssystem (LGS)__:

* $ a11 x_1 + a12 x_2 = b1 $
* $ a22 x_2 = b2 $

Schreiben Sie das LGS in __Matrizenschreibweise__ $ "A" * "x" = "b" $:
* $ "A" = #A $

    input cols := static

* $ "b" = #b $

Das LGS liegt bereits in Dreiecksform vor. Bestimmen Sie den __Lösungsvektor__ $"x"$:
* $ "x" = #x $


%%%


Lineare Gleichungssysteme

    A in MM(3 x 3 | { 1, 2, 3 } )
    b in MM(3 x 1 | { 0, 1 } )
    homogen := is_zero(b)

Gegeben sei das folgende LGS in Marizenschreibweise:
$A*x = b$

Ist die folgende Aussage korrekt?
[homogen] Das LGS ist __homogen__.



%%%


Lösungsmenge

Ein lineares Gleichungssytem ist durch den Gaußalgorithmus bereits in Dreiecksform gebracht worden. Die erweiterte Koeffizienenmatrix sieht folgendermaßen aus:

    a := { -3, -2, -1, 1, 2, 3 }
    A in MM(3 x 3 | a )
    b in MM(3 x 1 | a )
    A := triu(A)

$ augmented(A|b) $

Welche Aussage über die __Lösungsmenge__ ist korrekt?
(x) Es gibt eine eindeutige Lösung $x in RR^n$.
( ) Es gibt keine Lösung.
( ) Die Lösungsmenge kann mit einer freien Variable beschrieben werden.


%%%


Lösungsmenge

Ein lineares Gleichungssytem ist durch den Gaußalgorithmus bereits in Dreiecksform gebracht worden. Die erweiterte Koeffizienenmatrix sieht folgendermaßen aus:

    a := { -3, -2, -1, 1, 2, 3 }
    A in MM(3 x 3 | a )
    b in MM(3 x 1 | a )
    A := triu(A)
    A[3,3] := 0
    b[3,1] := 0

$ augmented(A|b) $

Welche Aussage über die __Lösungsmenge__ ist korrekt?
( ) Es gibt eine eindeutige Lösung $x in RR^n$.
( ) Es gibt keine Lösung.
(x) Die Lösungsmenge kann mit einer freien Variable beschrieben werden.


%%%


Lösungsmenge

Ein lineares Gleichungssytem ist durch den Gaußalgorithmus bereits in Dreiecksform gebracht worden. Die erweiterte Koeffizienenmatrix sieht folgendermaßen aus:

    a := { -3, -2, -1, 1, 2, 3 }
    A in MM(3 x 3 | a )
    b in MM(3 x 1 | a )
    A := triu(A)
    A[3,3] := 0

$ augmented(A|b) $

Welche Aussage über die __Lösungsmenge__ ist korrekt?
( ) Es gibt eine eindeutige Lösung $x in RR^n$.
(x) Es gibt keine Lösung.
( ) Die Lösungsmenge kann mit einer freien Variable beschrieben werden.


%%%


Gaußalgorithmus

Lösen Sie das folgende lineare Gleichungssystem mit Hilfe des __Gaußalgorithmus__:

    a := { 1, 2, 4 }
    A in MM(2 x 2 | a, invertible )
    b in MM(2 x 1 | a )
    x := linsolve(A,b)

$ augmented(A|b) $

Lösungsvektor: $"x"=#x$


%%%


Gaußalgorithmus

Lösen Sie das folgende lineare Gleichungssystem mit Hilfe des __Gaußalgorithmus__:

    a := { 1, 2, 4 }
    A in MM(3 x 3 | a, invertible )
    b in MM(3 x 1 | a )
    x := linsolve(A,b)

$ augmented(A|b) $

Lösungsvektor: $"x"=#x$


%%%


Gaußalgorithmus

Lösen Sie das folgende lineare Gleichungssystem mit Hilfe des __Gaußalgorithmus__:

    a := { 0, 1, 2 }
    A in MM(4 x 4 | a, invertible )
    b in MM(4 x 1 | a )
    x := linsolve(A,b)

$ augmented(A|b) $

Lösungsvektor: $"x"=#x$


%%%


Lineare Unabhängigkeit

	a, b, c, d, e, f, g, h, _i in { 0, 1, 2 }

	v1 := [[a],[b]]
	v2 := [[c],[d]]
	A := [[a,c],[b,d]]
	q1 := det(A) != 0

	a := (a + 1) mod 2
	v3 := [[a],[b]]
	v4 := [[c],[d]]
	A := [[a,c],[b,d]]
	q2 := det(A) != 0

	v5 := [[a],[b],[c]]
	v6 := [[d],[e],[f]]
	v7 := [[g],[h],[_i]]
	A := [[a,d,g],[b,e,h],[c,f,_i]]
	q3 := det(A) != 0

Welche der unten stehenden Vektoren sind __linear unabhängig__?

[q1] $ v1, v2 $
[q2] $ v3, v4 $
[q3] $ v5, v6, v7 $


%%%


Rang

	a := { 0, 1, 2 }
	A in MM( 3 x 3 | a )
	r := rank(A)

Sei $ "A" = A $ eine 3 x 3 Matrix über $ RR $.
Bestimmen Sie den __Rang__:

* $ rg("A") = #r $


%%%


Inverse Matrix

	a := { 0, 1, 2 }
	A in MM( 3 x 3 | a, invertible )
	iA := inv(A)

Berechnen Sie die __inverse__ Matrix:

* $ A^(-1) = #iA $


%%%


Determinante

	a := { -5, -4, ..., 5 }
	A in MM( 3 x 3 | a )
	A[1,1] := 0
	A[1,2] := 0
	d := det(A)

Sei $ "A" = A $ eine 3 x 3 Matrix über $ RR $.
Berechnen Sie die __Determinante__:

* $ det("A") = #d $









STOP

%%%

TODO: p153ff Lin. Unabh, Basis, ...


Lineare Unabhängigkeit

    a := { 1, 2, 3 }
    v1, v2 in MM(2 x 1 | a )
    A := [ v1, v2 ]


    a11 := v1[1,1]
    a21 := v1[2,1]
    a12 := v1[1,2]
    a22 := v1[2,2]
    A := [[a11,a21],[a21,a22]]
    s1 := det(A) != 0
    s2 := not s1

Sind die beiden Vektoren $v_1=v1$, $v_2=v2 \in RR^2$ linear __unabhängig__?
(s1) ja
(s2) nein

$A$

_Hinweis: Stellen Sie ein passendes lineares Gleichungssystem auf._
